group 'org.kotlin'
version '1.0-SNAPSHOT'
buildscript {
    ext.kotlin_version = '1.1.2-2'
    repositories {
        mavenCentral()
        maven {
            url "http://dl.bintray.com/ajoberstar/maven"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.ajoberstar:gradle-git:1.5.1'
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'application'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

sourceSets {
    main.kotlin.srcDirs += 'src'
}

mainClassName = 'VerifyAllSamplesKt'

asciidoctor {
    sourceDir file("docs-src/")
    sources {
        include 'index.adoc'
    }

    resources {
        from "$projectDir/docs-src/lib"
        into 'lib'
    }

    attributes version: project.version,
            highlightjsdir: "lib/highlight"
}


githubPages {
    repoUri = 'https://github.com/Kotlin/kotlinbyexample.git'
    targetBranch = 'master'
    pages {
        from "build/asciidoc/html5/"
        into "docs/"
    }
}


task verifyAllSamples(type:JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'VerifyAllSamplesKt'
}

task prepareMarkCode(type:Exec) {
    commandLine 'mkdir', 'src/docsOut'

}

task runMarkCode(type:Exec) {
    commandLine 'tools/mark-code/bin/mark-code', 'docs-src' ,'src/docsOut', '-o'
}